&ИзменениеИКонтроль("ТекстИнструкции")
Функция ВИ_ТекстИнструкции(СсылкаНаИнструкцию, УстановитьСтильОформления)

#Удаление
	HTMLКодИнструкции = СсылкаНаИнструкцию.ТекстИнструкции.Получить(); 
#КонецУдаления
#Вставка

	Если СсылкаНаИнструкцию.ВИ_ВнешняяИнструкция Тогда
	
		// Внешняя инструкция
		ВремяНачала = ОценкаПроизводительности.НачатьЗамерВремени();

		HTMLКодИнструкции = ВИ_ТекстВнешнейИнструкции(СсылкаНаИнструкцию.ВИ_АдресВнешнейИнструкции);
		
		ОценкаПроизводительности.ЗакончитьЗамерВремени("ВнешниеИнструкцииПолучениеТекстаВнешнейИнструкции", ВремяНачала);
		
	Иначе
		
		// Типовой механизм
		HTMLКодИнструкции = СсылкаНаИнструкцию.ТекстИнструкции.Получить(); 
	
	КонецЕсли;

#КонецВставки

	Если HTMLКодИнструкции <> Неопределено Тогда
		Если УстановитьСтильОформления Тогда
			Возврат РаботаСИнструкциями.УстановитьСтильОформленияИнструкции(HTMLКодИнструкции);
		Иначе
			Возврат HTMLКодИнструкции;
		КонецЕсли;
	Иначе
		Возврат "";
	КонецЕсли;

КонецФункции

&ИзменениеИКонтроль("ТекстыИнструкций")
Функция ВИ_ТекстыИнструкций(Инструкции, УстановитьСтильОформления)

	Результат = "";

	МассивТекстовИнструкций = Новый Массив;
	МассивСсылокНаИнструкции = Новый Массив;
#Вставка
	ВсеСтилиCSS = "";
#КонецВставки

	Для Каждого Инструкция Из Инструкции Цикл

		HTMLКодИнструкции = ТекстИнструкции(Инструкция, Ложь);
#Вставка
		Если Инструкция.ВИ_ВнешняяИнструкция Тогда
			
			СтилиИзHTML = ВИ_ИзвлечьСтилиИзHTML(HTMLКодИнструкции);
			
			Если НЕ ПустаяСтрока(СтилиИзHTML) 
				И СтрНайти(ВсеСтилиCSS, СтилиИзHTML) = 0 Тогда
				ВсеСтилиCSS = ВсеСтилиCSS + СтилиИзHTML;
			КонецЕсли;

			HTMLКодИнструкции = СтрЗаменить(HTMLКодИнструкции, 
											"<body class=""mceContentBody", 
											"<div class=""mceContentBody");
			HTMLКодИнструкции = СтрЗаменить(HTMLКодИнструкции, 
											"</body></body>", 
											"</div></body>");
			
		КонецЕсли;
#КонецВставки
		HTMLКодИнструкции = ТелоИнструкцииИзHTML(HTMLКодИнструкции);

		ТекстИнструкции = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		"<p class=""elem""><a name=""%1""></a></p>%2",
		Инструкция.УникальныйИдентификатор(), HTMLКодИнструкции);
		МассивТекстовИнструкций.Добавить(ТекстИнструкции);

		СсылкаНаИнструкцию = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		"<a href=""#%1"">%2</a>",
		Инструкция.УникальныйИдентификатор(),
		Инструкция.Наименование);
		МассивСсылокНаИнструкции.Добавить(СсылкаНаИнструкцию);
	КонецЦикла;

	Если МассивТекстовИнструкций.Количество() > 0 Тогда
		Если МассивТекстовИнструкций.Количество() = 1 Тогда
			Результат = МассивТекстовИнструкций[0];
#Удаление
			Результат = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				"<html><head></head><body>%1</body></html>", Результат);
#КонецУдаления			
#Вставка
			Результат = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				"<!DOCTYPE html>
				|<html><head>%1</head><body>%2</body></html>", ВсеСтилиCSS, Результат);
#КонецВставки
		ИначеЕсли МассивТекстовИнструкций.Количество() > 1 Тогда
			Ссылки = СтрСоединить(МассивСсылокНаИнструкции, "");
			Ссылки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"<p class=""nav"">%1</p>", Ссылки);
			СтрокаВНачало = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"<a href=""#nav"" class=""up"">%1</a>",
			НСтр("ru = 'В начало'"));
			Результат = СтрСоединить(МассивТекстовИнструкций, СтрокаВНачало) + СтрокаВНачало;
#Удаление
			Результат = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				"<html><head></head><body><a name=""nav""></a>%1%2</body></html>",
				Ссылки,
				Результат);
#КонецУдаления			
#Вставка
			Результат = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				"<html><head>%1</head><body><a name=""nav""></a>%2%3</body></html>",
				ВсеСтилиCSS,
				Ссылки,
				Результат);
#КонецВставки
		КонецЕсли;

		Если УстановитьСтильОформления Тогда
			РаботаСИнструкциями.УстановитьСтильОформленияИнструкции(Результат);
		КонецЕсли;
	КонецЕсли;

	Возврат Результат;

КонецФункции

// Возвращает HTML код содержимого страницы по URL.
//
// Параметры:
//	АдресСайта - Строка - URL
//
// Возвращаемое значение:
//	Строка - HTML код страницы.
//
Функция ВИ_ТекстВнешнейИнструкции(Знач АдресСайта)
	
	Попытка
		
		СтруктураСайта = ВИ_РазобратьАдресСайта(АдресСайта);
		
		Соединение = Новый HTTPСоединение(СтруктураСайта.Сервер, 
											СтруктураСайта.Порт, 
											, 
											, 
											, 
											10, 
											Новый ЗащищенноеСоединениеOpenSSL);
		
		Запрос = Новый HTTPЗапрос(СтруктураСайта.АдресСкрипта);
		
		Ответ = Соединение.Получить(Запрос); 
		
		ТекстОтвета = Ответ.ПолучитьТелоКакСтроку();
		
		// Формируем базовый URL
		БазовыйURL = ?(СтруктураСайта.Порт = 443, "https://", "http://");
		БазовыйURL = БазовыйURL + СтруктураСайта.Сервер;
		Если (СтруктураСайта.Порт <> 443 
			И СтруктураСайта.Порт <> 80) Тогда
			БазовыйURL = БазовыйURL + ":" + Формат(СтруктураСайта.Порт, "ЧГ=");
		КонецЕсли;
		
		// преобразуем все относительные URL в абсолютные
		ТекстОтвета = ВИ_ЗаменитьОтносительныеПутиВURL(ТекстОтвета, БазовыйURL);
		
		Возврат ТекстОтвета;
		
	Исключение
		
		ЗаписьЖурналаРегистрации("ВнешниеИнструкции.Получение текста внешней инструкции", 
									УровеньЖурналаРегистрации.Ошибка, 
									, 
									, 
									ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));

		Возврат "Инструкция по адресу " + АдресСайта +  " недоступна. Обратитесь к Администратору";
		
	КонецПопытки;
	
КонецФункции

// Формирует структуру параметров подключения к сайту по URL.
//
// Параметры:
//	АдресСайта - Строка - URL
//
// Возвращаемое значение:
//	Структура - Структура настроек подключения.
//
Функция ВИ_РазобратьАдресСайта(АдресСайта)
	
	АдресСайта = СокрЛП(АдресСайта); 
	
	Сервер = ""; 
	
	Порт = 0;
	
	АдресСкрипта = "";
	
	ЗащищенноеСоединение = Ложь;
	
	Если НЕ ПустаяСтрока(АдресСайта) Тогда
		
		АдресСайта = СтрЗаменить(АдресСайта, "\", "/");
		АдресСайта = СтрЗаменить(АдресСайта, " ", "");
		
		Если НРег(Лев(АдресСайта, 7)) = "http://" Тогда
			АдресСайта = Сред(АдресСайта, 8);
		ИначеЕсли НРег(Лев(АдресСайта, 8)) = "https://" Тогда
			АдресСайта = Сред(АдресСайта, 9);
			ЗащищенноеСоединение = Истина;
		Иначе
			// Ничего не делаем
		КонецЕсли;
		
		ПозицияСлэша = СтрНайти(АдресСайта, "/");
		
		Если ПозицияСлэша > 0 Тогда
			Сервер = Лев(АдресСайта, ПозицияСлэша - 1);
			АдресСкрипта = Прав(АдресСайта, СтрДлина(АдресСайта) - ПозицияСлэша);
		Иначе	
			Сервер = АдресСайта;
			АдресСкрипта = "";
		КонецЕсли;
		
		ПозицияДвоеточия = СтрНайти(Сервер, ":");
		ПортСтрока = "0";
		Если ПозицияДвоеточия > 0 Тогда
			СерверСПортом = Сервер;
			Сервер = Лев(СерверСПортом, ПозицияДвоеточия - 1);
			ПортСтрока = Прав(СерверСПортом, СтрДлина(СерверСПортом) - ПозицияДвоеточия);
		КонецЕсли;
		
		ОписаниеТипа = Новый ОписаниеТипов("Число");
		Порт = ОписаниеТипа.ПривестиЗначение(ПортСтрока);
		
		Если Порт = 0 Тогда
			Порт = ?(ЗащищенноеСоединение, 443, 80);
		КонецЕсли;
		
	КонецЕсли;
	
	НастройкиПодключения = Новый Структура;
	НастройкиПодключения.Вставить("Сервер", Сервер); 
	НастройкиПодключения.Вставить("Порт", Порт);
	НастройкиПодключения.Вставить("АдресСкрипта", АдресСкрипта);
	НастройкиПодключения.Вставить("ЗащищенноеСоединение", ЗащищенноеСоединение);
	
	Возврат НастройкиПодключения;
	
КонецФункции

// Преобразует все относительные URL в HTML-коде в абсолютные
// на основе указанного базового адреса сайта.
//
// Параметры:
//  ТекстHTML - Строка - Исходный HTML-код страницы, в котором необходимо
//                       заменить относительные пути к ресурсам.
//  БазовыйURL - Строка - Базовый адрес сайта вида "http(s)://hostname[:порт]" без
//                        завершающего слэша, который будет подставлен перед относительными путями.
//
// Возвращаемое значение:
//  Строка - HTML-код, в котором относительные пути в поддерживаемых
//           атрибутах заменены на абсолютные URL.
//
Функция ВИ_ЗаменитьОтносительныеПутиВURL(ТекстHTML, БазовыйURL)
	
	РезультатHTML = ТекстHTML;
	
	РезультатHTML = ВИ_ЗаменитьОтносительныеПутиВАтрибутах(РезультатHTML, "src", БазовыйURL);
	РезультатHTML = ВИ_ЗаменитьОтносительныеПутиВАтрибутах(РезультатHTML, "href", БазовыйURL);
	РезультатHTML = ВИ_ЗаменитьОтносительныеПутиВАтрибутах(РезультатHTML, "data-image-src", БазовыйURL);
	
	Возврат РезультатHTML;
	
КонецФункции

// Заменяет относительные пути в указанном HTML-атрибуте на абсолютные URL.
//
// Параметры:
//  ТекстHTML - Строка - HTML-код, в котором требуется заменить относительные пути
//                       в значениях указанного атрибута.
//  ИмяАтрибута - Строка - Имя HTML-атрибута, в значениях которого выполняется поиск
//                         относительных путей (например, "src", "href", "data-image-src").
//  БазовыйURL - Строка - Базовый адрес сайта вида "http(s)://hostname[:порт]" без
//                        завершающего слэша, который будет подставлен перед относительным путём.
//
// Возвращаемое значение:
//  Строка - HTML-код, в котором относительные пути в указанном атрибуте
//            заменены на абсолютные URL.
//
Функция ВИ_ЗаменитьОтносительныеПутиВАтрибутах(ТекстHTML, ИмяАтрибута, БазовыйURL)
	
	РезультатHTML = ТекстHTML;
	
	// Заменяем атрибут="/путь" на атрибут="https://site.com/путь"
	// Двойные кавычки
	РезультатHTML = СтрЗаменить(РезультатHTML, 
								ИмяАтрибута + "=""/", 
								ИмяАтрибута + "=""" + БазовыйURL + "/");
	
	// Одинарные кавычки
	РезультатHTML = СтрЗаменить(РезультатHTML, 
								ИмяАтрибута + "'='/", 
								ИмяАтрибута + "'='" + БазовыйURL + "/");
	
	Возврат РезультатHTML;
	
КонецФункции

// Извлекает содержимое секции <head> из HTML-кода инструкции.
//
// Параметры:
//  HTMLКодИнструкции - Строка - Полный HTML-код отдельной инструкции (страницы), содержащий
//                               секцию <head> с определениями стилей и других ресурсов.
//
// Возвращаемое значение:
//  Строка - HTML-фрагмент, соответствующий содержимому секции <head>
//           без самих тегов <head> и </head>. Может быть напрямую
//           вставлен в раздел <head> результирующего HTML-документа.
//
Функция ВИ_ИзвлечьСтилиИзHTML(HTMLКодИнструкции) Экспорт
	
	ЧтениеHTML = Новый ЧтениеHTML;
	ЧтениеHTML.УстановитьСтроку(HTMLКодИнструкции);
	ПостроительDOM = Новый ПостроительDOM;
	ДокументDOM = ПостроительDOM.Прочитать(ЧтениеHTML);
	
	ЭлементHEAD = ДокументDOM.ПолучитьЭлементыПоИмени("head").Элемент(0);
	ЗаписьDOM = Новый ЗаписьDOM; 
	ЗаписьHTML = Новый ЗаписьHTML; 
	ЗаписьHTML.УстановитьСтроку(); 
	ЗаписьDOM.Записать(ЭлементHEAD, ЗаписьHTML); 
	Результат = ЗаписьHTML.Закрыть();
	
	// Убираем теги <head> и </head>
	Результат = СтрЗаменить(Результат, "<head>", "");
	Результат = СтрЗаменить(Результат, "</head>", "");
	Результат = СтрЗаменить(Результат, Символы.ПС, "");
	Результат = СтрЗаменить(Результат, Символы.ВК, "");
	
	Возврат Результат;
	
КонецФункции
