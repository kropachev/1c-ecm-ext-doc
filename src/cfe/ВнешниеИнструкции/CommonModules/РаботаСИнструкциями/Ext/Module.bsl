
// Возвращает HTML код содержимого страницы по URL.
//
// Параметры:
//	АдресСайта - Строка, URL
//
// Возвращаемое значение:
//	Строка - HTML код страницы.
//
Функция ВИ_КодВнешнейИнструкцииHTML(Знач АдресСайта)
	
	Попытка
		
		СтруктураСайта = ВИ_РазобратьАдресСайта(АдресСайта);
		
		Соединение = Новый HTTPСоединение(СтруктураСайта.Сервер, 
											СтруктураСайта.Порт, 
											, 
											, 
											, 
											10, 
											Новый ЗащищенноеСоединениеOpenSSL);
		
		Запрос = Новый HTTPЗапрос(СтруктураСайта.АдресСкрипта);
		
		Ответ = Соединение.Получить(Запрос); 
		
		ТекстОтвета = Ответ.ПолучитьТелоКакСтроку();
		
		Возврат ТекстОтвета;
		
	Исключение
		
		ЗаписьЖурналаРегистрации("ВнешниеИнструкции.Получение текста внешней инструкции", 
									УровеньЖурналаРегистрации.Ошибка, 
									, 
									, 
									ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));

		Возврат "Инструкция по адресу " + АдресСайта +  " недоступна. Обратитесь к Администратору";
		
	КонецПопытки;
	
КонецФункции

// Формирует структуру параметров подключения к сайту по URL.
//
// Параметры:
//	АдресСайта - Строка, URL
//
// Возвращаемое значение:
//	Структура - Структура настроек подключения.
//
Функция ВИ_РазобратьАдресСайта(АдресСайта)
	
	АдресСайта = СокрЛП(АдресСайта); 
	
	Сервер = ""; 
	
	Порт = 0;
	
	АдресСкрипта = "";
	
	ЗащищенноеСоединение = Ложь;
	
	Если НЕ ПустаяСтрока(АдресСайта) Тогда
		
		АдресСайта = СтрЗаменить(АдресСайта, "\", "/");
		АдресСайта = СтрЗаменить(АдресСайта, " ", "");
		
		Если НРег(Лев(АдресСайта, 7)) = "http://" Тогда
			АдресСайта = Сред(АдресСайта, 8);
		ИначеЕсли НРег(Лев(АдресСайта, 8)) = "https://" Тогда
			АдресСайта = Сред(АдресСайта, 9);
			ЗащищенноеСоединение = Истина;
		Иначе
			// Ничего не делаем
		КонецЕсли;
		
		ПозицияСлэша = СтрНайти(АдресСайта, "/");
		
		Если ПозицияСлэша > 0 Тогда
			Сервер = Лев(АдресСайта, ПозицияСлэша - 1);
			АдресСкрипта = Прав(АдресСайта, СтрДлина(АдресСайта) - ПозицияСлэша);
		Иначе	
			Сервер = АдресСайта;
			АдресСкрипта = "";
		КонецЕсли;
		
		ПозицияДвоеточия = СтрНайти(Сервер, ":");
		ПортСтрока = "0";
		Если ПозицияДвоеточия > 0 Тогда
			СерверСПортом = Сервер;
			Сервер = Лев(СерверСПортом, ПозицияДвоеточия - 1);
			ПортСтрока = Прав(СерверСПортом, СтрДлина(СерверСПортом) - ПозицияДвоеточия);
		КонецЕсли;
		
		ОписаниеТипа = Новый ОписаниеТипов("Число");
		Порт = ОписаниеТипа.ПривестиЗначение(ПортСтрока);
		
		Если Порт = 0 Тогда
			Порт = ?(ЗащищенноеСоединение, 443, 80);
		КонецЕсли;
		
	КонецЕсли;
	
	НастройкиПодключения = Новый Структура;
	НастройкиПодключения.Вставить("Сервер", Сервер); 
	НастройкиПодключения.Вставить("Порт", Порт);
	НастройкиПодключения.Вставить("АдресСкрипта", АдресСкрипта);
	НастройкиПодключения.Вставить("ЗащищенноеСоединение", ЗащищенноеСоединение);
	
	Возврат НастройкиПодключения;
	
КонецФункции

&ИзменениеИКонтроль("ТекстИнструкции")
Функция ВИ_ТекстИнструкции(СсылкаНаИнструкцию, УстановитьСтильОформления)

#Удаление
	HTMLКодИнструкции = СсылкаНаИнструкцию.ТекстИнструкции.Получить(); 
#КонецУдаления
#Вставка

	Если СсылкаНаИнструкцию.ВИ_ВнешняяИнструкция Тогда
	
		// Внешняя инструкция
		HTMLКодИнструкции = ВИ_КодВнешнейИнструкцииHTML(СсылкаНаИнструкцию.ВИ_АдресВнешнейИнструкции);
		
	Иначе
		
		// Типовой механизм
		HTMLКодИнструкции = СсылкаНаИнструкцию.ТекстИнструкции.Получить(); 
	
	КонецЕсли;

#КонецВставки

	Если HTMLКодИнструкции <> Неопределено Тогда
		Если УстановитьСтильОформления Тогда
			Возврат РаботаСИнструкциями.УстановитьСтильОформленияИнструкции(HTMLКодИнструкции);
		Иначе
			Возврат HTMLКодИнструкции;
		КонецЕсли;
	Иначе
		Возврат "";
	КонецЕсли;

КонецФункции
