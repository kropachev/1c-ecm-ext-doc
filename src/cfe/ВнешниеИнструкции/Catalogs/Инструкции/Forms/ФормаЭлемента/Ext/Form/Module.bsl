
&НаСервере
&После("ПриСозданииНаСервере")
Процедура ВИ_ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Поле формы "ВИ_ВнешняяИнструкция"
	НовыйЭлемент = ЭтотОбъект.Элементы.Вставить("ВИ_ВнешняяИнструкция", 
												Тип("ПолеФормы"), 
												ЭтотОбъект.Элементы.ГруппаОсновныхРеквизитов, 
												ЭтотОбъект.Элементы.Активна);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеПереключателя;
	НовыйЭлемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
	НовыйЭлемент.ПутьКДанным = "Объект.ВИ_ВнешняяИнструкция";
	НовыйЭлемент.ВидФлажка = ВидФлажка.Выключатель;
	НовыйЭлемент.УстановитьДействие("ПриИзменении", "ВИ_ВнешняяИнструкцияПриИзменении");

	// Поле формы "ВИ_АдресВнешнейИнструкции"
	НовыйЭлемент = ЭтотОбъект.Элементы.Вставить("ВИ_АдресВнешнейИнструкции", 
												Тип("ПолеФормы"), 
												ЭтотОбъект.Элементы.ГруппаОсновныхРеквизитов, 
												ЭтотОбъект.Элементы.Активна);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.ПутьКДанным = "Объект.ВИ_АдресВнешнейИнструкции";
	НовыйЭлемент.УстановитьДействие("ПриИзменении", "ВИ_АдресВнешнейИнструкцииПриИзменении");

	
	Элементы.ВИ_АдресВнешнейИнструкции.Видимость = Объект.ВИ_ВнешняяИнструкция;
	Элементы.ГруппаКнопокЗагрузить.Видимость = Не Объект.ВИ_ВнешняяИнструкция;
	
КонецПроцедуры

&НаКлиенте
Процедура ВИ_ВнешняяИнструкцияПриИзменении(Элемент)
	
	Элементы.ВИ_АдресВнешнейИнструкции.Видимость = Объект.ВИ_ВнешняяИнструкция;
	Элементы.ГруппаКнопокЗагрузить.Видимость = Не Объект.ВИ_ВнешняяИнструкция;
	
	ВИ_ЗаполнитьТекстВнешнейИнструкции();
	
КонецПроцедуры

&НаКлиенте
Процедура ВИ_АдресВнешнейИнструкцииПриИзменении(Элемент)
	
	ВИ_ЗаполнитьТекстВнешнейИнструкции()
	
КонецПроцедуры

&НаКлиенте
Процедура ВИ_ЗаполнитьТекстВнешнейИнструкции()
	
	ТекстИнструкции = "<html><head></head><body></body></html>";
	
	Если Объект.ВИ_ВнешняяИнструкция
		И ЗначениеЗаполнено(Объект.ВИ_АдресВнешнейИнструкции) Тогда
		ВИ_ЗаполнитьТекстВнешнейИнструкцииНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВИ_ЗаполнитьТекстВнешнейИнструкцииНаСервере()
	
	HTMLКодИнструкции = РаботаСИнструкциями.ВИ_ТекстВнешнейИнструкции(Объект.ВИ_АдресВнешнейИнструкции);
	ТекстИнструкции = РаботаСИнструкциями.УстановитьСтильОформленияИнструкции(HTMLКодИнструкции);
	
КонецПроцедуры
